apply plugin: 'com.android.application'

android {
    def version = project.getProperties().get("version")
    if (version == null || "unspecified".equalsIgnoreCase(version)) {
        version = "TEST"
    }
    def gitCommitHash = 'git rev-parse --short HEAD'.execute().text.trim()
    def gitBranchName = 'git rev-parse --abbrev-ref HEAD'.execute().text.trim()
    def gitCommitCount = "git rev-list --count HEAD".execute().text.trim().toInteger()
    def buildTime = new Date().format("yyyy-MM-dd HH:mm:ss", TimeZone.getTimeZone("Asia/Shanghai"))

    compileSdk 34
    defaultConfig {
        applicationId "io.github.lazyimmortal.sesame"
        minSdk 21
        //noinspection ExpiredTargetSdkVersion
        targetSdk 31
        versionCode gitCommitCount
        versionName version

        buildConfigField "String", "GIT_COMMIT_HASH", "\"${gitCommitHash}\""
        buildConfigField "String", "GIT_BRANCH_NAME", "\"${gitBranchName}\""
        buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""
    }
    ext {
        applicationName = "Sesame"
        applicationType = "Normal"
    }
    flavorDimensions.add("default")
    productFlavors {
        normal {
            dimension "default"
            ext.applicationType = "Normal"
        }
        compatible {
            dimension "default"
            ext.applicationType = "Compatible"
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    applicationVariants.configureEach { variant ->
        variant.outputs.configureEach { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                if (variant.buildType.name == 'release') {
                    output.outputFileName = "${applicationName}-${variant.productFlavors[0].ext.applicationType}-${defaultConfig.versionName}.apk"
                }
            }
        }
    }
    sourceSets {
        main {
            jniLibs.srcDirs = ['src/main/jniLibs']
        }
    }
}

dependencies {
    //noinspection GradleCompatible
    implementation 'com.android.support:appcompat-v7:28.0.0'
    compileOnly 'de.robv.android.xposed:api:82'
    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.32'
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.32'
    implementation 'com.elvishew:xlog:1.11.0'
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'

    normalImplementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.17.1'
    normalImplementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.17.1'
    normalImplementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.17.1'

    compatibleImplementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.13.5'
    compatibleImplementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.13.5'
    compatibleImplementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.13.5'

}
